plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

group = 'dev.fwcd.mcdiscordbridge'
version = '0.2.0'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven {
        name = 'spigot-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven { url 'https://repo.mikeprimm.com' }
}

dependencies {
    implementation 'net.dv8tion:JDA:5.0.0-beta.15'
    implementation 'club.minnced:discord-webhooks:0.8.4'
    implementation 'commons-io:commons-io:2.14.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    compileOnly 'us.dynmap:dynmap-api:3.6'
    compileOnly 'org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT'
}

jar {
    enabled = false
}

shadowJar {
    def newBasePackage = "${project.group}.dependencies"
    def depPackages = [
        "com.fasterxml",
        "com.iwebpp",
        "com.neovisionaries",
        "com.google",
        "net.dv8tion",
        "club.minnced",
        "gnu.trove",
        "kotlin",
        "okhttp3",
        "okio",
        "org.apache",
        "org.intellij",
        "org.jetbrains",
        "org.json",
        "org.slf4j",
        "tomp2p",
    ]

    for (depPackage in depPackages) {
        relocate depPackage, "${newBasePackage}.${depPackage}"
    }
}
